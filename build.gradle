buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
}
repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    mavenCentral()
    maven { url "https://dl.bintray.com/soywiz/soywiz" }
}
def ktor_version = '1.1.3'
def logback_version = '1.2.3'
def coroutines_version = '1.3.7'
def serialization_version = '0.20.0'
def klockVersion = "1.4.0"
final js_generated_sources = 'build/generated_sources_project_properties'

kotlin {
    jvm()
    js {
        compilations.main.compileKotlinTask.doFirst {
            def dir = file(js_generated_sources)
            dir.mkdirs()
            def output = new java.io.File(dir, "generated1.kt")
            def content = file("generated_template.kt").text.replace("{BUILDDATE}", "${new Date()}")
            output.text = content
            output.append("\nval resources = mapOf<String,String>(\n")

            def widgets = file("src/jsMain/resources/widgets")
            def sep = ""
            widgets.list().each {
                println("adding resource $it")
                def resource = file(widgets.path + '/' + it)
                output.append("$sep\"$it\" to \"${resource.text.bytes.encodeBase64().toString()}\"")
                sep = ",\n"
            }
            output.append("\n)\nprivate val installResources = { window.asDynamic()[\"widget_resources\"] = resources }()\n")
        }
        browser {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
                implementation "com.soywiz:klock-metadata:$klockVersion"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                implementation "com.soywiz:klock-jvm:$klockVersion"
                implementation 'net.sf.biweekly:biweekly:0.6.3'
                implementation 'com.h2database:h2:1.4.200'
                implementation 'org.jetbrains.exposed:exposed:0.17.7'
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
                implementation "com.soywiz:klock-js:$klockVersion"
            }
            kotlin.srcDirs += js_generated_sources
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

//jvmJar {
//    dependsOn(jsBrowserProductionWebpack)
//    //includes compiled js file
//    from(new File(jsBrowserProductionWebpack.entry.name, jsBrowserProductionWebpack.outputPath))
//}

task run(type: JavaExec, dependsOn: [jvmJar]) {
    group = "application"
    main = "sample.JvmMainKt"
    classpath(configurations.jvmRuntimeClasspath, jvmJar)
    args = []
}

task runSample(type: JavaExec, dependsOn: [jvmJar]) {
    group = "application"
    main = "sample.SampleJvmKt"
    classpath(configurations.jvmRuntimeClasspath, jvmJar)
    args = []
}